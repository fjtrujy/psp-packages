pkgname=freetype2
pkgver=2.11.0
pkgrel=1
pkgdesc="a freely available software library to render fonts"
arch=('mips')
url="https://www.freetype.org/"
license=('custom:Freetype' 'GPL2')
depends=('zlib' 'bzip2' 'libpng')
makedepends=()
optdepends=()
group=('all')
source=("https://github.com/freetype/freetype/archive/VER-${pkgver//./-}.tar.gz")
sha256sums=('fffcba25febd1581cc12d706a39bee96f37eab627d0838ce6e8189ebd841d722')

build() {
    cd "freetype-VER-${pkgver//./-}"
    mkdir -p build
    cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp -DBUILD_SHARED_LIBS=OFF \
        "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "freetype-VER-${pkgver//./-}/build"
    make --quiet $MAKEFLAGS install
    cd ..

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 LICENSE.TXT "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 docs/GPLv2.TXT "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 docs/FTL.TXT "$pkgdir/psp/share/licenses/$pkgname"
}

