# Maintainer: Wouter Wijsman <wwijsman@live.nl>
pkgname=zlib
pkgver=1.2.11
pkgrel=1
pkgdesc="Compression library implementing the deflate compression method found in gzip and PKZIP"
arch=('mips')
url="https://www.zlib.net/"
license=('ZLIB')
depends=()
makedepends=()
optdepends=()
source=("https://github.com/madler/zlib/archive/v${pkgver}.tar.gz")
sha256sums=('629380c90a77b964d896ed37163f5c3a34f6e6d897311f1df2a7016355c45eff')

build() {
    cd "${pkgname}-${pkgver}"
    mkdir -p build
    cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp -DBUILD_SHARED_LIBS=OFF \
        -DUNIX:BOOL=ON "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "${pkgname}-${pkgver}/build"
    make --quiet $MAKEFLAGS install || { exit 1; }

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 ../README "$pkgdir/psp/share/licenses/$pkgname"

    # Install documentation
    mkdir -p "$pkgdir/psp/share/doc/"
    cp -r ../doc "$pkgdir/psp/share/doc/$pkgname"
}

