# Maintainer: Wouter Wijsman <wwijsman@live.nl>
pkgname=zziplib
pkgver=0.13.72
pkgrel=1
pkgdesc="provides read access to zipped files in a zip-archive"
arch=('mips')
url="http://zziplib.sourceforge.net/"
license=('ZLIB')
depends=('zlib')
makedepends=()
optdepends=()
source=("git+https://github.com/gdraheim/zziplib.git#commit=a82f98f94a2cc365fad71eee02d7dd50c0810844")
sha256sums=('SKIP')

build() {
    cd "$pkgname"
    mkdir -p build
    cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp -DBUILD_SHARED_LIBS=OFF \
        -DBUILD_STATIC_LIBS=ON -DZZIPSDL=OFF -DZZIPTEST=OFF -DZZIPDOCS=OFF "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "$pkgname/build"
    make --quiet $MAKEFLAGS install
    cd ..

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 COPYING.LIB "$pkgdir/psp/share/licenses/$pkgname"
}

