# Contributor: Wouter Wijsman <wwijsman@live.nl>
pkgname=jsoncpp
pkgver=1.9.4
pkgrel=1
pkgdesc="a C++ library for interacting with JSON"
arch=('mips')
license=('unlicense')
url="https://github.com/open-source-parsers/jsoncpp"
makedepends=()
source=("git+https://github.com/fjtrujy/jsoncpp#commit=29f9853455002bba46262ded9c1b57656796af75")
sha256sums=('SKIP')

build() {
  cd $pkgname
  mkdir -p build
  cd build
  cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp -DBUILD_SHARED_LIBS=OFF \
        -DCMAKE_BUILD_TYPE=Release -DJSONCPP_WITH_POST_BUILD_UNITTEST=OFF -DJSONCPP_WITH_TESTS=OFF "${XTRA_OPTS[@]}" .. || { exit 1; }
  make --quiet $MAKEFLAGS || { exit 1; }
}

package () {
  cd "$pkgname/build"
  make --quiet $MAKEFLAGS install
  cd ..

  mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
  install -m 644 LICENSE "$pkgdir/psp/share/licenses/$pkgname"
}
